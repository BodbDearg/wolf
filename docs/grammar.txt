Module:
	[0..N: DeclDef|,] EOF

DeclDef:
	Func
	VarDecl

Func:
	func Identifier ( [0..N: FuncArg ,][FuncArg] ) [-> Type] [do] Scope end

FuncArg:
	Identifier : Type

Type:
	PrimitiveType
	^|? Type
	[ AssignExpr ] Type

PrimitiveType:
	void
	any
	nullptr
	int|int8|int16|int32|int64|int128
	uint|uint8|uint16|uint32|uint64|uint128
	bool
	string

Scope:
    [0..N: Stmnt|,]

Stmnt:
	NoOpStmnt
    PrintStmnt
    AssertStmnt
    VarDecl
    IfStmnt
    WhileStmnt
    LoopStmnt
    ScopeStmnt
	BreakStmnt
	NextStmnt
	ReturnStmnt
	AssignExpr

NoOpStmnt:
    noop

PrintStmnt:
    print ( AssignExpr )

AssertStmnt:
    assert ( AssignExpr )

VarDecl:
    let Identifier = AssignExpr
    let Identifier : Type = AssignExpr

IfStmnt:
	if|unless AssignExpr [then] Scope end
    if|unless AssignExpr [then] Scope else [do] Scope end
	if|unless AssignExpr [then] Scope else IfStmnt

WhileStmnt:
	while|until AssignExpr [do] Scope end

LoopStmnt:
    loop Scope end
	loop Scope repeat while|until AssignExpr

ScopeStmnt:
	scope Scope end

BreakStmnt:
	break
	break if|unless AssignExpr

NextStmnt:
	next
	next if|unless AssignExpr

ReturnStmnt:
	return
	return AssignExpr
    return if|unless AssignExpr
    return AssignExpr if|unless AssignExpr

AssignExpr:
    TernaryExpr
    TernaryExpr = AssignExpr
    TernaryExpr += AssignExpr
    TernaryExpr -= AssignExpr
	TernaryExpr |= AssignExpr
	TernaryExpr ^= AssignExpr
    TernaryExpr *= AssignExpr
    TernaryExpr /= AssignExpr
    TernaryExpr %= AssignExpr
	TernaryExpr &= AssignExpr
	TernaryExpr <<= AssignExpr
	TernaryExpr >>= AssignExpr
	TernaryExpr >>>= AssignExpr

TernaryExpr:
	LOrExpr
	LOrExpr ? AssignExpr : AssignExpr

LOrExpr:
	LAndExpr
	LAndExpr or LOrExpr

LAndExpr:
	NotExpr
	NotExpr and LAndExpr

NotExpr:
	CmpExpr
	not NotExpr
	~ NotExpr

CmpExpr:
	AddExpr
	AddExpr == CmpExpr
	AddExpr is CmpExpr
	AddExpr != CmpExpr
	AddExpr is not CmpExpr
	AddExpr < CmpExpr
	AddExpr <= CmpExpr
	AddExpr > CmpExpr
	AddExpr >= CmpExpr

AddExpr:
	MulExpr
	MulExpr + AddExpr
	MulExpr - AddExpr
	MulExpr | AddExpr
	MulExpr ^ AddExpr

MulExpr:
	ShiftExpr
	ShiftExpr * MulExpr
	ShiftExpr / MulExpr
	ShiftExpr % MulExpr
	ShiftExpr & MulExpr

ShiftExpr:
	PrefixExpr
	PrefixExpr << ShiftExpr
	PrefixExpr >> ShiftExpr
	PrefixExpr >>> ShiftExpr

PrefixExpr:
	PostfixExpr
	+ PrefixExpr
	- PrefixExpr
	& PrefixExpr
	$ PrefixExpr

PostfixExpr:
	CastExpr
	CastExpr ++
	CastExpr --
	PostfixExpr ( [0..N: AssignExpr ,][AssignExpr] )
	PostfixExpr [ AssignExpr ]

CastExpr:
	PrimaryExpr
	cast ( AssignExpr to Type )

PrimaryExpr:
	IntLit
	BoolLit
	StrLit
	NullLit
    ArrayLit
    Identifier
    ReadnumExpr
    TimeExpr
    RandExpr
    ( AssignExpr )

ArrayLit:
    [ [0..N: AssignExpr ,] [AssignExpr] ]

ReadnumExpr:
    readnum ( )

TimeExpr:
	time ( )

RandExpr:
    rand ( )
	srand ( AssignExpr )
