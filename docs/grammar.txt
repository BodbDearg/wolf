Module:
    Scope <EOF>

Scope:
    Stmnts

Stmnts:
    Stmnt
    Stmnt Stmnts

Stmnt:
	NoOpStmnt
    PrintStmnt
    VarDecl
    IfStmnt
    AssignExpr

NoOpStmnt:
    nop

PrintStmnt:
    print ( AssignExpr )

VarDecl:
    var Identifier = AssignExpr

IfStmnt:
	if AssignExpr then Scope end
	if AssignExpr then Scope else IfStmnt
	if AssignExpr then Scope else Scope end

AssignExpr:
    OrExpr
    OrExpr = AssignExpr

OrExpr:
	AndExpr
	AndExpr or OrExpr

AndExpr:
	NotExpr
	NotExpr and AndExpr

NotExpr:
	EqExpr
	not NotExpr

EqExpr:
	RelExpr
	RelExpr == EqExpr
	RelExpr != EqExpr

RelExpr:
	AddSubExpr
	AddSubExpr < RelExpr
	AddSubExpr <= RelExpr
	AddSubExpr > RelExpr
	AddSubExpr >= RelExpr

AddSubExpr:
	MulDivExpr
	MulDivExpr + AddSubExpr
	MulDivExpr - AddSubExpr

MulDivExpr:
	UnaryExpr
	UnaryExpr * MulDivExpr
	UnaryExpr / MulDivExpr

UnaryExpr:
	PrimaryExpr
	-PrimaryExpr
	+PrimaryExpr
	(AssignExpr)

PrimaryExpr:
	IntLit
	BoolLit
	StrLit
    Identifier
    ReadnumExpr

ReadnumExpr:
    readnum ( )
